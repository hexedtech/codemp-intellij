plugins {
	id 'java'
	id 'org.jetbrains.intellij.platform' version '2.0.1'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'com.palantir.git-version' version '3.1.0'
}

group = 'mp.code'
//version = versionDetails().lastTag

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	intellijPlatform {
		defaultRepositories()
	}
}

dependencies {
	implementation 'org.slf4j:slf4j-api:2.0.9'
	implementation 'ch.qos.logback:logback-classic:1.4.12'
	implementation files('../../lib/dist/java/build/libs/codemp-0.6.2.jar')
	compileOnly 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'

	intellijPlatform { // TODO: not all of these may be needed, but whatever
		intellijIdeaCommunity('2023.3')
		bundledPlugin("com.intellij.java")
		pluginVerifier()
		zipSigner()
		instrumentationTools()
	}
}

shadowJar {
	archiveClassifier.set('')
	dependencies {
		include(dependency(files('../../lib/java/build/libs/codemp-0.6.2.jar')))
	}
}

tasks {
	patchPluginXml {
		sinceBuild.set('222')
		untilBuild.set('233.*')
	}

	signPlugin {
		certificateChain.set(System.getenv('CERTIFICATE_CHAIN'))
		privateKey.set(System.getenv('PRIVATE_KEY'))
		password.set(System.getenv('PRIVATE_KEY_PASSWORD'))
	}

	publishPlugin {
		token.set(System.getenv('PUBLISH_TOKEN'))
	}
}

instrumentedJar.dependsOn shadowJar //TODO: instrumentedJar should use fatjar as input
